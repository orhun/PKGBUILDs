# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Constantine Fedotov <zenflak@gmail.com>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-hack
pkgver=0.6.24
_commit=64499a33bf4a49f950f6840c5288880de912dcbc
pkgrel=1
pkgdesc="A cargo subcommand to provide various options useful for testing and CI"
arch=('x86_64')
url="https://github.com/taiki-e/cargo-hack"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo' 'git')
source=("$pkgname::git+$url.git#commit=$_commit"
        "Cargo.lock")
sha512sums=('eb172904ebe0a95527bc0a2522ed56ea426d84d2d1b7dc3246ec6c9412b483d0293870137a3d44733eed6972144e5d7e3eef7fa0f493f78c4f8f87bee91383bd'
            'dc8514055781731cb7107ab7a8099a4b3a7725112bea622458136203ae19ce71aa5f6e0b5f9fa6728e8923e335763511046b06d878c592249024b8d1ecb5cbf9')

prepare() {
  cd "$pkgname"
  cp "$srcdir/Cargo.lock" .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen -- \
    --skip "multi_target" \
    --skip "version_range" \
    --skip "version_range_failure"
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
  depends+=(rustup)
}

# vim: ts=2 sw=2 et:
